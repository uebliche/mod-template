buildscript {
    if (gradle.ext.has("mcmetaPluginAvailable") && gradle.ext.mcmetaPluginAvailable) {
        repositories {
            mavenCentral()
        }
        def mcmetaVersion = (project.findProperty("mcmetaPluginVersion")
                ?: System.getenv("MCMETA_PLUGIN_VERSION")
                ?: "0.1.0").toString()
        dependencies {
            classpath "net.uebliche:mcmeta-gradle:${mcmetaVersion}"
        }
    }
}

plugins {
    id 'base'
}

def resolveMinecraftVersion = { Project project ->
    def candidates = [
            project.findProperty("mcVersion"),
            project.findProperty("minecraft_version"),
            project.rootProject.findProperty("mcVersion"),
            project.rootProject.findProperty("minecraft_version")
    ]
    def resolved = candidates.find { it }
    if (!resolved && project.rootProject.ext.has("mcmetaMinecraftVersion")) {
        def mcmetaValue = project.rootProject.ext.mcmetaMinecraftVersion
        if (mcmetaValue != null && mcmetaValue.toString().trim()) {
            resolved = mcmetaValue
        }
    }
    resolved?.toString()
}

def mcmetaAvailable = gradle.ext.has("mcmetaPluginAvailable") && gradle.ext.mcmetaPluginAvailable
if (mcmetaAvailable) {
    try {
        apply plugin: 'net.uebliche.mcmeta'

        def requestedMcVersion = resolveMinecraftVersion(project)
        mcmeta {
            autoLoad = false
            if (requestedMcVersion) {
                minecraftVersion = requestedMcVersion.toString()
            }
        }
        mcmeta.resolveNow(project)
    } catch (Exception ex) {
        logger.lifecycle("⚠️  mcmeta plugin unavailable: ${ex.message}")
    }
}

subprojects {
    apply plugin: 'java-library'

    repositories {
        mavenCentral()
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        maven {
            name = 'NeoForged'
            url = 'https://maven.neoforged.net/releases'
        }
        maven {
            name = 'PaperMC'
            url = 'https://repo.papermc.io/repository/maven-public/'
        }
    maven {
        name = 'Velocity'
        url = 'https://repo.velocitypowered.com/releases/'
        content {
            includeGroup 'com.velocitypowered'
        }
    }
    maven {
        name = 'VelocitySnapshots'
        url = 'https://repo.velocitypowered.com/snapshots/'
        content {
            includeGroup 'com.velocitypowered'
        }
    }
    }

    afterEvaluate { proj ->
        proj.tasks.withType(JavaCompile).configureEach {
            options.encoding = "UTF-8"
        }
    }
}
