pluginManagement {
    repositories {
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        maven {
            name = 'NeoForged'
            url = 'https://maven.neoforged.net/releases'
        }
        maven {
            name = 'PaperMC'
            url = 'https://repo.papermc.io/repository/maven-public/'
        }
        mavenCentral()
        gradlePluginPortal()
    }
}

def resolveMcmetaPluginDir = {
    def override = gradle.startParameter.projectProperties.get("mcmetaPluginPath")?.trim()
    if (!override) {
        override = System.getenv("MCMETA_PLUGIN_PATH")
    }
    if (override) {
        def candidate = new File(override)
        if (!candidate.isAbsolute()) {
            candidate = new File(settingsDir, override)
        }
        return candidate
    }
    def candidates = [
            new File(settingsDir, "../../tools/mcmeta-gradle/gradle-plugin"),
            new File(settingsDir, "../../web/mcmeta/gradle-plugin"),
            new File(settingsDir, "../../../mcmeta/gradle-plugin")
    ]
    return candidates.find { it.exists() }
}

def mcmetaPluginDir = resolveMcmetaPluginDir()
def mcmetaPluginAvailable = mcmetaPluginDir != null && mcmetaPluginDir.exists()
if (mcmetaPluginAvailable) {
    includeBuild(mcmetaPluginDir) {
        dependencySubstitution {
            substitute(module("net.uebliche:mcmeta-gradle")).using(project(":"))
        }
    }
}

gradle.ext.mcmetaPluginAvailable = mcmetaPluginAvailable
gradle.ext.mcmetaPluginDir = mcmetaPluginDir

def onlyLoader = gradle.startParameter.projectProperties.get("onlyLoader")?.trim()
def loaders = ["fabric", "neoforge", "paper", "velocity"]
if (onlyLoader) {
	def normalized = onlyLoader.toLowerCase()
	if (normalized.startsWith("loader-")) {
		normalized = normalized.substring("loader-".length())
	}
	if (normalized == "forge") {
		normalized = "neoforge"
	}
	if (!loaders.contains(normalized)) {
		throw new GradleException("Unknown onlyLoader '${onlyLoader}'. Expected one of: ${loaders}")
	}
	include("common", "loader-${normalized}")
} else {
	include("common", "loader-fabric", "loader-neoforge", "loader-paper", "loader-velocity")
}
