pluginManagement {
    def resolveMcmetaPluginDir = {
        def override = gradle.startParameter.projectProperties.get("mcmetaPluginPath")?.trim()
        if (!override) {
            override = System.getenv("MCMETA_PLUGIN_PATH")
        }
        if (override) {
            def candidate = new File(override)
            if (!candidate.isAbsolute()) {
                candidate = new File(settingsDir, override)
            }
            return candidate
        }
        return new File(settingsDir, "../../web/mcmeta/gradle-plugin")
    }

    def mcmetaPluginDir = resolveMcmetaPluginDir()
    def mcmetaPluginAvailable = mcmetaPluginDir != null && mcmetaPluginDir.exists()
    if (mcmetaPluginAvailable) {
        includeBuild(mcmetaPluginDir)
    }
    repositories {
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        maven {
            name = 'NeoForged'
            url = 'https://maven.neoforged.net/releases'
        }
        maven {
            name = 'PaperMC'
            url = 'https://repo.papermc.io/repository/maven-public/'
        }
        mavenCentral()
        gradlePluginPortal()
    }
    gradle.ext.mcmetaPluginAvailable = mcmetaPluginAvailable
}

def onlyLoader = gradle.startParameter.projectProperties.get("onlyLoader")?.trim()
def loaders = ["fabric", "forge", "paper", "velocity"]
if (onlyLoader) {
	def normalized = onlyLoader.toLowerCase()
	if (normalized.startsWith("loader-")) {
		normalized = normalized.substring("loader-".length())
	}
	if (!loaders.contains(normalized)) {
		throw new GradleException("Unknown onlyLoader '${onlyLoader}'. Expected one of: ${loaders}")
	}
	include("common", "loader-${normalized}")
} else {
	include("common", "loader-fabric", "loader-forge", "loader-paper", "loader-velocity")
}
